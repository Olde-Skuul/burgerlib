;
; PowerPC32 assembly for Metrowerks tool chain
;
; Copyright 1995-2023 by Rebecca Ann Heineman becky@burgerbecky.com
;
; Build with Metrowerks Codewarrior 10 for MacOSX Carbon
;
; uint_t Burger::is_infinite(float fInput) BURGER_NOEXCEPT
; r0, r3-12, fp0-fp13 are volatile
; fInput = fp1
; Result in r3
;

	IMPORT g_fInf__6Burger

	EXPORT is_infinite__6BurgerFf[DS]
	EXPORT .is_infinite__6BurgerFf[PR]

	TOC
	tc		is_infinite__6BurgerFf[TC], is_infinite__6BurgerFf[DS]
	tc		g_fInf__6Burger[TC],g_fInf__6Burger

	CSECT	is_infinite__6BurgerFf[DS]
	dc.l	.is_infinite__6BurgerFf[PR]
	dc.l	TOC[tc0]

	CSECT	.is_infinite__6BurgerFf[PR]

; Get the address of g_fInf
	lwz		r3, g_fInf__6Burger[TC](RTOC)

; Strip the sign
	fabs	fp1, fp1

; Fetch fInf
	lfs		fp0, 0(r3)

; Compare for equality
	fcmpu	cr0, fp1, fp0

; Move cr0 into r0
	mfcr	r0

; (Flags>>29)&1 Grab the "Equal" flag
	extrwi	r3, r0, 1, 2
	blr

	END
