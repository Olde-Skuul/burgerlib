;
; PowerPC32 assembly for Metrowerks tool chain
;
; Copyright 1995-2023 by Rebecca Ann Heineman becky@burgerbecky.com
;
; Build with Metrowerks Codewarrior 10 for MacOSX Carbon
;
; float Burger::modulo_radians(float fInput) BURGER_NOEXCEPT
; r0, r3-12, fp0-fp13 are volatile
; fInput = fp1
; Result in fp1
;

	IMPORT	g_fReciprocalPi2__6Burger
	IMPORT	g_fHalf__6Burger
	IMPORT	g_fNegPi2__6Burger

	EXPORT modulo_radians__6BurgerFf[DS]
	EXPORT .modulo_radians__6BurgerFf[PR]

	TOC
	tc		modulo_radians__6BurgerFf[TC], modulo_radians__6BurgerFf[DS]
	tc		g_fReciprocalPi2__6Burger[TC],g_fReciprocalPi2__6Burger
	tc		g_fHalf__6Burger[TC],g_fHalf__6Burger
	tc		g_fNegPi2__6Burger[TC],g_fNegPi2__6Burger

	CSECT	modulo_radians__6BurgerFf[DS]
	dc.l	.modulo_radians__6BurgerFf[PR]
	dc.l	TOC[tc0]

	CSECT	.modulo_radians__6BurgerFf[PR]

; Fetch the pointers
	lwz		r3, g_fReciprocalPi2__6Burger[TC](RTOC)

; Create 0x4330000080000000 for integer to float conversions
	lis		r6, 0x4330
	lwz		r4, g_fHalf__6Burger[TC](RTOC)
	lis		r7, 0x8000
	lwz		r5, g_fNegPi2__6Burger[TC](RTOC)

; Load in 1/ 2Pi
	lfs		fp3, 0(r3)

; Load in 0.5f
	lfs		fp4, 0(r4)

; Load in -Pi2
	lfs		fp5, 0(r5)

; (fInput*g_fReciprocalPi2)+g_fHalf
	fmadds	fp3, fp1, fp3, fp4

; Store 0x4330000080000000
	stw		r6, -16(SP)
	stw		r7, -12(SP)

; Load 0x4330000080000000 in FPU
	lfd		fp2, -16(SP)

; Convert to integer
	fctiwz	fp0, fp3

; Store the integer in memory (64 bit)
	stfd	fp0, -8(SP)

; Extract the low word
	lwz		r0, -4(SP)

; Flip the bit
	xor		r0, r0, r7

; Create a fake double
	stw		r6, -8(SP)

; Store the integer
	stw		r0, -4(SP)

; Load the rounded double
	lfd		fp0, -8(SP)

; Complete the int to float conversion
	fsubs	fp2, fp0, fp2

; Compare the two and get rid of the prerounded
	fcmpu	cr0, fp2, fp3
	ble		FModule1

; Load 1.0f (0.5 + 0.5)
	fadds	fp0, fp4, fp4

; Fixup
	fsubs	fp2, fp2, fp0

; Don't use fnmsubs, it doesn't handle the sign bit properly
; (fVar*-g_fPi2) + fInput
FModule1:
	fmadds	fp1, fp2, fp5, fp1

; Exit
	blr

	END
