;
; PowerPC32 assembly for Metrowerks tool chain
;
; Copyright 1995-2023 by Rebecca Ann Heineman becky@burgerbecky.com
;
; Build with Metrowerks Codewarrior 10 for MacOSX Carbon
;
; uint_t Burger::is_finite(double dInput) BURGER_NOEXCEPT
; r0, r3-12, fp0-fp13 are volatile
; dInput = fp1
; Result in r3
;

	IMPORT g_dInf__6Burger

	EXPORT is_finite__6BurgerFd[DS]
	EXPORT .is_finite__6BurgerFd[PR]

	TOC
	tc		is_finite__6BurgerFd[TC], is_finite__6BurgerFd[DS]
	tc		g_dInf__6Burger[TC],g_dInf__6Burger

	CSECT	is_finite__6BurgerFd[DS]
	dc.l	.is_finite__6BurgerFd[PR]
	dc.l	TOC[tc0]

	CSECT	.is_finite__6BurgerFd[PR]

; Get the address of g_dInf
	lwz		r3, g_dInf__6Burger[TC](RTOC)

; Strip the sign
	fabs	fp1, fp1

; Fetch g_dInf
	lfd		fp0, 0(r3)

; Compare for equality
	fcmpu	cr0, fp1, fp0

; Move cr0 into r0
	mfcr	r0

; (Flags>>31)&1 Grab the "Less than" flag
	extrwi	r3, r0, 1, 0
	blr

	END
