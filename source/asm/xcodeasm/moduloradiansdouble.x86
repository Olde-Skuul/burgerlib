/***************************************

	Assembly for MacOSX Darwin

	Copyright 1995-2023 by Rebecca Ann Heineman becky@burgerbecky.com

	Build with XCode tool chain

	XCode uses a C++ parser and will try to assemble this file on all
	CPU types, so test for the proper CPU type before invoking any cpu
	instructions.

	double BURGER_API Burger::modulo_radians(double dInput) BURGER_NOEXCEPT

***************************************/

/***************************************

	PowerPC 32/64 version

	r0, r3-12, fp0-fp13 are volatile
	Result in fp1
	fInput = fp1

***************************************/

#if defined(__ppc__) || defined(__ppc64__)
	.align 2
	.text

	.globl __ZN6Burger14modulo_radiansEd
__ZN6Burger14modulo_radiansEd:

/* Fetch the pointers */
	lis		r3, ha16(__ZN6Burger16g_dReciprocalPi2E)

/* Create 0x4330000080000000 for integer to float conversions */
	lis		r6, 0x4330
	lis		r4, ha16(__ZN6Burger7g_dHalfE)
	lis		r7, 0x8000
	lis		r5, ha16(__ZN6Burger9g_dNegPi2E)

/* Load in 1/ 2Pi */
	lfd		f3, lo16(__ZN6Burger16g_dReciprocalPi2E)(r3)

/* Load in 0.5f */
	lfd		f4, lo16(__ZN6Burger7g_dHalfE)(r4)

/* (fInput*g_fReciprocalPi2) */
	fmul	f3, f1, f3

/* Load in Pi2 */
	lfd		f5, lo16(__ZN6Burger9g_dNegPi2E)(r5)

/* (fInput*g_fReciprocalPi2)+g_fHalf */
	fadd	f3, f3, f4

/* fmadd doesn't handle sign properly so it failed the unit test */
/* Used explicit fmul and fadd to get the accuracy */
/* (fInput*g_fReciprocalPi2)+g_fHalf */
/*	fmadd	f3, f1, f3, f4 */

/* Store 0x4330000080000000 */
	stw		r6, -16(r1)
	stw		r7, -12(r1)

/* Load 0x4330000080000000 in FPU */
	lfd		f2, -16(r1)

/* Convert to integer */
	fctiwz	f0, f3

/* Store the integer in memory (64 bit) */
	stfd	f0, -8(r1)

/* Extract the low word */
	lwz		r0, -4(r1)

/* Flip the bit */
	xor		r0,r0,r7

/* Create a fake double */
	stw		r6,-8(r1)

/* Store the integer */
	stw		r0,-4(r1)

/* Load the rounded double */
	lfd		f0,-8(r1)

/* Complete the int to float conversion */
	fsub 	f2,f0,f2

/* Compare the two and get rid of the pre-rounded */
	fcmpu 	cr0,f2,f3
	ble		1f

/* Load 1.0f */
	fadd	f0, f4, f4

/* Fixup */
	fsub	f2, f2, f0

/* Don't use fnmsubs, it doesn't handle the sign bit properly */
/* (fVar*-g_fPi2) + fInput */
1:	fmadd	f1, f2, f5, f1
	blr

#endif

/***************************************

	Intel 32 bit version

	eax, ecx and edx are volatile
	Result in 8087 FPU
	fInput = 4(%esp)

***************************************/

#if defined(__i386__)
	.align	4,0x90
	.globl __ZN6Burger14modulo_radiansEd
__ZN6Burger14modulo_radiansEd:

/* Load into the FPU */
	movsd		4(%esp), %xmm0

/* Load in 1/ 2Pi */
	movsd		(__ZN6Burger16g_dReciprocalPi2E), %xmm2

/* Multiply (Really fInput/2Pi) */
	mulsd		%xmm0, %xmm2

/* Add half for rounding */
	addsd		(__ZN6Burger7g_dHalfE), %xmm2

/* Convert to integer */
	cvttsd2si	%xmm2, %eax
	cvtsi2sdl	%eax, %xmm1

/* Compare the two and get rid of the pre-rounded */
	ucomisd		%xmm2, %xmm1

/* Did it round up? */
	jbe			1f

/* Fixup */
	subsd		(__ZN6Burger6g_dOneE), %xmm1

/* Mul by 2 pi */
1:	mulsd		(__ZN6Burger6g_dPi2E), %xmm1

/* Subtract and clean up */
	subsd		%xmm1, %xmm0

/* Return in st(0) */
	movsd		%xmm0, 4(%esp)
	fldl		4(%esp)
	ret

#endif

/***************************************

	ARM 64 version

	x0-x15, v0-v7, v16-v31 are volatile
	dInput = v0
	Result in v0

***************************************/

#if defined(__aarch64__)

	.align	4
	.globl __ZN6Burger14modulo_radiansEd
__ZN6Burger14modulo_radiansEd:

; Fetch the pointers and constants
; d1 = g_dReciprocalPi2, d2 = 0.5, d3 = 1.0, d4 = g_dPi2
	mov		x8, #0xC883
	mov		x9, #0x2D18
	fmov	d2, #0.5
	movk	x8, #0x6DC9, lsl #16
	movk	x9, #0x5444, lsl #16
	fmov	d3, #1.0
	movk	x8, #0x5F30, lsl #32
	movk	x9, #0x21FB, lsl #32
	movk	x8, #0x3FC4, lsl #48
	movk	x9, #0x4019, lsl #48

; Get g_dReciprocalPi2
	fmov	d1, x8

; (fInput * g_dReciprocalPi2 + 0.5f
; Do not use the fmadd instruction
; It fails unit tests due to sign being flipped
;	fmadd	d1, d0, d1, d2
	fmul	d1, d0, d1
	fadd	d1, d1, d2

; Convert to integer and back again
; dVar is d2
	fcvtzs	d2, d1

; Get g_dPi2
	fmov	d4, x9
	scvtf	d2, d2

; Subtract 1.0f for next integer
	fsub	d3, d2, d3

; If (dVar > dTemp) dVar = d3
	fcmp	d1, d2
	fcsel	d1, d3, d2, mi

; dInput - (dVar * g_dPi2)
	fmsub	d0, d1, d4, d0
	ret

#endif
