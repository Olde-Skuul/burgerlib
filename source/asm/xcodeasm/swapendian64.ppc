/***************************************

	Assembly for MacOSX Darwin

	Copyright 1995-2023 by Rebecca Ann Heineman becky@burgerbecky.com

	Build with XCode tool chain

	XCode uses a C++ parser and will try to assemble this file on all
	CPU types, so test for the proper CPU type before invoking any cpu
	instructions.

	extern "C" uint64_t _swapendian64(uint64_t uInput) BURGER_NOEXCEPT

***************************************/

/***************************************

	PowerPC 32/64 version
	
	r0, r3-12, fp0-fp13 are volatile
	Result in r3/r4 or r3
	uInput = r3/r4 or r3

***************************************/

#if defined(__ppc__) || defined(__ppc64__)
	.align 2
	.text

	.globl __swapendian64
__swapendian64:

/* PowerPC 64 has is all in one register, split it */
#if defined(__ppc64__)
	srdi	r4, r3, 32
#endif

/* Perform the endian swap on both 32 bit registers */

/* 4,1,2,3 <- 1,2,3,4 */
	rlwinm	r0, r3, 24, 0, 31
	rlwinm	r12, r4, 24, 0, 31

/* 4,3,2,3 <- 1,2,3,4 (Only copy bits 8-15) */
	rlwimi	r0, r3, 8, 8, 15
	rlwimi	r12, r4, 8, 8, 15

/* 4,3,2,1 <- 1,2,3,4 (Only copy bits 24-31) */
	rlwimi	r0, r3, 8, 24, 31
	rlwimi	r12, r4, 8, 24, 31

/* Return them, word swapped! */
#if defined(__ppc64__)
	sldi	r3, r0, 32
	or		r3, r3, r12
#else
	mr		r3, r12
	mr		r4, r0
#endif
	blr

#endif
