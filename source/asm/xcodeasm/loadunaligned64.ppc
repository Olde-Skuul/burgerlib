/***************************************

	Assembly for MacOSX Darwin

	Copyright 1995-2023 by Rebecca Ann Heineman becky@burgerbecky.com

	Build with XCode tool chain

	XCode uses a C++ parser and will try to assemble this file on all
	CPU types, so test for the proper CPU type before invoking any cpu
	instructions.

	uint64_t BURGER_API Burger::_load_unaligned(const uint64_t* pInput) BURGER_NOEXCEPT

***************************************/

/***************************************

	PowerPC 32/64 version
	
	r0, r3-12, fp0-fp13 are volatile
	Result in r3 or r3/r4
	pInput = r3

***************************************/

#if defined(__ppc__) || defined(__ppc64__)
	.align 2
	.text

	.globl __ZN6Burger15_load_unalignedEPKy
__ZN6Burger15_load_unalignedEPKy:

/* r7-r10 will get the upper 32 bits */
	lbz		r10, 0(r3)
	lbz		r9, 1(r3)
	lbz		r8, 2(r3)
	lbz		r7, 3(r3)

/* r6-r3 will get the lower 32 bits */
	lbz		r6, 4(r3)
	rlwimi	r7, r8, 8, 16, 23
	lbz		r5, 5(r3)
	rlwimi	r7, r9, 16, 8, 15
	lbz		r4, 6(r3)
	rlwimi	r7, r10, 24, 0, 7
	lbz		r3, 7(r3)

/* Merge the lower 32 bits */
	rlwimi	r3, r4, 8, 16, 23
	rlwimi	r3, r5, 16, 8, 15
	rlwimi	r3, r6, 24, 0, 7

#if defined(__ppc64__)
/* Return the value in r3 */
	sldi	r7, r7, 32
	or		r3, r3, r7
#else
/* Return the value in r3,r4 */
	mr		r4, r3
	mr		r3, r7
#endif
	blr

#endif
