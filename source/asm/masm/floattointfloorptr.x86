;
; X86 assembly for Visual Studio
;
; Copyright 1995-2023 by Rebecca Ann Heineman becky@burgerbecky.com
;
; Build with ml.exe
;
; void BURGER_API Burger::float_to_int_floor(int32_t* pOutput, float fInput)
; eax, ecx and edx are volatile
; pOutput = ecx
; fInput = [esp+4]
;

	.686
	.MODEL FLAT

_TEXT segment para 'CODE'

	align 16
public ?float_to_int_floor@Burger@@YIXPAHM@Z
?float_to_int_floor@Burger@@YIXPAHM@Z proc

; Get the input value
	fld		dword ptr[esp + 4]

; Load the same value in the FPU
	fld		st(0)

; Convert the float to integer
	frndint

; Store the integer (Leave a copy in the FPU)
	fist	dword ptr[esp + 4]

; Compare the integer and pop
	fcomip	st(0), st(1)

; Clean up the FP stack
	fstp	st(0)

; Get the integer
	mov		eax, dword ptr[esp + 4]

; Overflow? (Negative)
	jbe		NoExtra

; Round to zero
	dec		eax

; Store in the pointer
NoExtra:
	mov		dword ptr[ecx], eax
	ret		4

?float_to_int_floor@Burger@@YIXPAHM@Z endp

_TEXT ends

	end
