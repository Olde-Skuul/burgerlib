;
; X86 assembly for x86 Visual Studio compilers
;
; Build with ml.exe
;
; Burger::e8087Rounding __fastcall Burger::set_8087_rounding(e8087Rounding)
;

	.686

_TEXT segment para 'CODE'

	align 16
public ?set_8087_rounding@Burger@@YI?AW4e8087Rounding@1@W421@@Z
?set_8087_rounding@Burger@@YI?AW4e8087Rounding@1@W421@@Z proc

; eax, ecx, and edx are volatile

; Space for the status word
	sub		esp, 4

; Get the current status word
	fnstcw	[esp]

; Get the current value for return
	mov		eax, dword ptr[esp]

; Move to the Pentium bits area
	shl		ecx, 10

; Get the bits to retain for write
	mov		edx, eax

; Mask for return value
	and		eax, 0C00H

; Mask off unused bits
	and		edx, 0F3FFH

; Convert to enum
	shr		eax, 10

; Blend in the bits
	or		edx, ecx

; Store in memory
	mov		dword ptr[esp], edx

; Save the new status register
	fldcw	[esp]

; Fix stack
	add		esp, 4
	ret

?set_8087_rounding@Burger@@YI?AW4e8087Rounding@1@W421@@Z endp

_TEXT ends

	end
