;
; X86 assembly for Visual Studio
;
; Copyright 1995-2023 by Rebecca Ann Heineman becky@burgerbecky.com
;
; Build with ml.exe
;
; Burger::e8087Precision __fastcall Burger::set_8087_precision(e8087Precision uPrecision)
; eax, ecx and edx are volatile
; uPrecision = ecx
; Result in eax
;

	.686

_TEXT segment para 'CODE'

	align 16
public ?set_8087_precision@Burger@@YI?AW4e8087Precision@1@W421@@Z
?set_8087_precision@Burger@@YI?AW4e8087Precision@1@W421@@Z proc

; Space for the status word
	sub		esp, 4

; Get the current status word
	fnstcw	[esp]

; Get the current value for return
	mov		eax, dword ptr[esp]

; Move to the Pentium bits area
	shl		ecx, 8

; Get the bits to retain for write
	mov		edx, eax

; Mask for return value
	and		eax, 0300H

; Mask off unused bits
	and		edx, 0FCFFH

; Convert to enum
	shr		eax, 8

; Blend in the bits
	or		edx, ecx

; Store in memory
	mov		dword ptr[esp], edx

; Save the new status register
	fldcw	[esp]

; Fix stack
	add		esp, 4
	ret

?set_8087_precision@Burger@@YI?AW4e8087Precision@1@W421@@Z endp

_TEXT ends

	end
