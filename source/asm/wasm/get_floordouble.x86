;
; X86 assembly for Open Watcom
;
; Copyright 1995-2023 by Rebecca Ann Heineman becky@burgerbecky.com
;
; Build with wasm.exe
;
; double BURGER_API Burger::get_floor(double dInput) BURGER_NOEXCEPT
; all registers are non-volatile
; Result in 8087 FPU
; dInput = [esp+4]
;

	.686
	.MODEL FLAT,SYSCALL

	.data

; Actually Burger::g_u8087RoundToNegativeInfinity
	EXTRN	`W?g_u8087RoundToNegativeInfinity$:Burger$nxus`:WORD

_TEXT segment para 'CODE'

	align 16
public `W?get_floor$:Burger$n(d)d`
`W?get_floor$:Burger$n(d)d` proc

; Load the same value in the FPU
	fld		qword ptr[esp+4]

; Save the current 8087 control word
	fstcw	word ptr[esp+4]

; Force round to negative infinity
	fldcw	[`W?g_u8087RoundToNegativeInfinity$:Burger$nxus`]

; Perform the rounding
	frndint

; Restore the old 8087 control word
	fldcw	word ptr[esp+4]
	ret		8

`W?get_floor$:Burger$n(d)d` endp

_TEXT	ends

	end
