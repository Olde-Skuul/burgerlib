;
; X86 assembly for Open Watcom
;
; Copyright 1995-2025 by Rebecca Ann Heineman becky@burgerbecky.com
;
; Build with wasm.exe
;
; uint_t BURGER_API Burger::has_CPUID(void) BURGER_NOEXCEPT
; all registers are non-volatile
; Result in eax
;

	.686

_TEXT segment para 'CODE'

	align 16
public `W?has_CPUID$:Burger$n()ui`
`W?has_CPUID$:Burger$n()ui` proc

; See of flags bit 21 can be changed.
; If it can, it's higher than 486, which
; implies that CPUID is available

; Save flags on the stack
	pushfd

; Get in register and leave on stack
	mov		eax, dword ptr[esp]

; Switch bit 21 for the test
	xor		eax, 0x00200000

; Set the flags with the new value
	push	eax
	popfd

; Read in the flags into a register
	pushfd
	pop		eax

; Did the change "take"
	xor		eax, dword ptr[esp]

; Move to the lowest bit
	shr		eax, 21

; Set to TRUE or FALSE
	and		eax, 1

; Restore the flags to the original state
	popfd
	ret

`W?has_CPUID$:Burger$n()ui` endp

_TEXT ends

	end
