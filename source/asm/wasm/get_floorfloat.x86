;
; X86 assembly for Open Watcom
;
; Copyright 1995-2023 by Rebecca Ann Heineman becky@burgerbecky.com
;
; Build with wasm.exe
;
; float BURGER_API Burger::get_floor(float fInput) BURGER_NOEXCEPT
; all registers are non-volatile
; Result in 8087 FPU
; fInput = [esp+4]
;

	.686
	.MODEL FLAT,SYSCALL

	.data

; Actually Burger::g_u8087RoundToNegativeInfinity
	EXTRN	`W?g_u8087RoundToNegativeInfinity$:Burger$nxus`:WORD

_TEXT segment para 'CODE'

	align 16
public `W?get_floor$:Burger$n(b)b`
`W?get_floor$:Burger$n(b)b` proc

; Load the same value in the FPU
	fld		dword ptr[esp+4]

; Save the current 8087 control word
	fstcw	word ptr[esp+4]

; Force round to negative infinity
	fldcw	[`W?g_u8087RoundToNegativeInfinity$:Burger$nxus`]

; Perform the rounding
	frndint

; Restore the old 8087 control word
	fldcw	word ptr[esp+4]
	ret		4

`W?get_floor$:Burger$n(b)b` endp

_TEXT	ends

	end
